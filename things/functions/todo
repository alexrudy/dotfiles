#
#  todo
#  .dotfiles
#
#  Created by Alexander Rudy on 2016-05-31.
#  Copyright 2016 Alexander Rudy. All rights reserved.
#

LIST=${1:-"Today"}
osascript<<EOF
global newline
set newline to "
"
using terms from application "Things"
  
  on appendTags(thecontainer, thetags)
    if (count of {tags in thecontainer}) is greater than 0 then
      repeat with thistag in (tags of thecontainer)
        set thisname to name of thistag as text
        if thetags does not contain thisname then
          copy thisname to the end of thetags
        end if
      end repeat
    end if
  end appendTags
  
  on formatTodo(thetodo, counter)
    tell application "Things"
      set todotext to name of thetodo
      if (id of to dos of list "Today") contains id of thetodo then
        set todotext to "â˜… " & todotext
      end if
      set tagtext to " (+"
      set thesetags to {}
      set posttext to ""
      appendTags(thetodo, thesetags) of me
      if project of thetodo is not equal to missing value then
        set posttext to posttext & " @" & name of project of thetodo
        appendTags(project of thetodo, thesetags) of me
        if area of project of thetodo is not equal to missing value then
          set posttext to posttext & " / " & name of area of project of thetodo
          appendTags(area of project of thetodo, thesetags) of me
        end if
      else
        if area of thetodo is not equal to missing value then
          set posttext to posttext & " @" & name of area of thetodo
        end if
      end if
      if due date of thetodo is not equal to missing value then
        set daystodue to ((due date of thetodo) - (current date)) / (3600 * 24)
        if daystodue is less than 1.0 then
          set posttext to posttext & " !Due Today"
        else
          set ndays to round (daystodue)
          set posttext to posttext & " !Due in " & ndays & " days"
        end if
      end if
      set AppleScript's text item delimiters to {" +"}
      set tagtext to tagtext & (thesetags as text) & ")"
      if tagtext is not equal to " (+)" then
        set todotext to todotext & tagtext
      end if
      set todotext to todotext & posttext
      set innertodos to {}
      try
        set innertodos to (to dos of thetodo whose status is open)
      end try
      set innercounter to 0
      repeat with innertodo in innertodos
        if (id of to dos of list "Trash") does not contain (id of innertodo) then
          set innercounter to innercounter + 1
          set thiscounter to counter & "." & innercounter
          set todotext to todotext & newline & "   " & thiscounter & ") " & formatTodo(innertodo, thiscounter) of me
        end if
      end repeat
    end tell
    return todotext
  end formatTodo
  
  
  tell application "Things"
    set myList to {}
    set thelistname to "$LIST"
    if thelistname is in (name of lists) then
      set todos to (to dos of list thelistname whose status is open)
    else if thelistname is in (name of projects) then
      set todos to (to dos of project thelistname whose status is open)
    else if thelistname is in (name of areas) then
      set todos to (to dos of area thelistname whose status is open)
    else if thelistname is in (name of tags) then
      set todos to (to dos of tag thelistname whose status is open)
    else
      error "Can't find list '" & thelistname & "' in Things"
    end if
    if todos is not equal to {} and (count of todos) is greater than 0 then
      set counter to 0
      repeat with thetodo in todos
        if (id of to dos of list "Trash") does not contain (id of thetodo) then
          set counter to counter + 1
          set end of myList to (counter as text) & ") " & formatTodo(thetodo, counter as text) of me
        end if
      end repeat
      set AppleScript's text item delimiters to {"
"}
      get myList as text
    end if
  end tell
end using terms from
EOF
