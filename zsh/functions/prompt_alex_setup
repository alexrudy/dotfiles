#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   steeef <steeef@gmail.com>
#   Alex Rudy <alex.rudy@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_alex_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_alex_colors[1]}%b%f%u%c${_prompt_alex_colors[4]}●%f)"
  else
    branch_format="(${_prompt_alex_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  
  vcs_info 'prompt'
  things_info=""
  if [[ "$OSTYPE" == darwin* ]]; then
      if [[ $(osascript -e 'tell application "System Events" to (name of processes) contains "Things"') == "true" ]]; then
n=`osascript<<EOF
  tell application "Things"
      get count of ((to dos in list "Today") whose status is open)
  end tell
EOF
`
        things_info="${_prompt_alex_colors[1]}(✓$n)%f"
    fi
  fi
  
  if (( $+functions[python-info] )); then
    python-info
  fi
  
  
}

function prompt_alex_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_alex_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_alex_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_alex_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_alex_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_alex_colors[5]}%a%f)"
  local unstaged_format="${_prompt_alex_colors[2]}●%f"
  local staged_format="${_prompt_alex_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format "${_prompt_alex_colors[3]}(%v)%f"
  # Define prompts.
  PROMPT="
${SSH_TTY:+"${_prompt_alex_colors[3]}%n%f at ${_prompt_alex_colors[2]}%m%f "}in ${_prompt_alex_colors[5]}%~%f "'${vcs_info_msg_0_}'"
""› "
  RPROMPT='$python_info[virtualenv]$things_info'
}

prompt_alex_setup "$@"
