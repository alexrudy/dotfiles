#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   steeef <steeef@gmail.com>
#   Alex Rudy <alex.rudy@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_alex_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="on ${_prompt_alex_colors[1]}%b%f%u%c${_prompt_alex_colors[4]}‚óè%f"
  else
    branch_format="on ${_prompt_alex_colors[1]}%b%f%u%c"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  _need_push=$(need_push)
  vcs_info 'prompt'
  apple_info=""
  if [[ "$OSTYPE" == darwin* ]]; then
    apple_info="("
    if [[ $(isactive "Things3") == "true" ]]; then
        apple_info+="${_prompt_alex_colors[1]}‚úì$(today)%f"
    fi
    if [[ $(isactive "Tunnelblick") ]]; then
        if [[ $(tb connections) -ge 1 ]]; then
            apple_info+="üîê"
        else
            apple_info+="üîì"
        fi
    fi  
    # if [[ $(isactive "Mail") == "true" ]]; then
    #     unread=$(mail-unread)
    #     if [[ $unread -gt 0 ]]; then
    #         apple_info+="${_prompt_alex_colors[1]}‚úâÔ∏è $unread%f"
    #     fi
    # fi
    apple_info+=")"
  fi
  if [[ "$apple_info" == "()" ]]; then
      apple_info=""
  fi
  
  if (( $+functions[python-info] )); then
    python-info
  fi
  
  
}

function prompt_alex_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_alex_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_alex_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_alex_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="on ${_prompt_alex_colors[1]}%b%f%u%c"
  local action_format="(${_prompt_alex_colors[5]}%a%f)"
  local unstaged_format="${_prompt_alex_colors[2]}‚óè%f"
  local staged_format="${_prompt_alex_colors[5]}‚óè%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format "${_prompt_alex_colors[3]}(%v)%f"
  # Define prompts.
  if [[ -z $SSH_TTY && -z $TMUX ]]; then
      HOSTPROMPT=""
  else
      HOSTPROMPT="${_prompt_alex_colors[3]}%n%f at ${_prompt_alex_colors[2]}%m%f "
  fi
  PROMPT=$'\n'"%{$(iterm2_prompt_mark)%}${HOSTPROMPT}in ${_prompt_alex_colors[5]}%~%f "'${vcs_info_msg_0_}${_need_push}'$'\n'"‚Ä∫ "
  RPROMPT='$python_info[virtualenv]$apple_info'
}

prompt_alex_setup "$@"
